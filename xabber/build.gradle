apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'com.frogermcs.androiddevmetrics'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.1'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 638
        versionName '2.6.5(638)'
        manifestPlaceholders = [crashlytics:getLocalProperty("crashlytics.key")]
    }

    lintOptions {
        // problems with generated "applicationId" string resource
        disable 'MissingTranslation'
        disable 'InvalidPackage'

        abortOnError false
    }

    subprojects {
        afterEvaluate {
            if (getPlugins().hasPlugin('android') ||
                    getPlugins().hasPlugin('android-library')) {

                configure(android.lintOptions) {
                    abortOnError false

                }
            }
        }
    }

    flavorDimensions "build"
    productFlavors {

        open {
            dimension "build"
            applicationId "com.xabber.android.open"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

        dev {
            dimension "build"
            applicationId "com.xabber.androiddev"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

        beta {
            dimension "build"
            applicationId "com.xabber.android.beta"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

        prod {
            dimension "build"
            applicationId "com.xabber.android"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

        vip {
            dimension "build"
            applicationId "com.xabber.androidvip"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'mips', 'x86', 'x86_64'

            universalApk true
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

def build_param = "${build}";

if (build_param == "open") {
    // exclude all flavours except open
    android.variantFilter { variant ->
        if (!variant.getFlavors().get(0).name.equals('open')) {
            variant.setIgnore(true);
        }
    }
}

def getLocalProperty(String propertyName) {
    def propsFile = rootProject.file('local.properties')
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        return props[propertyName]
    } else {
        return ""
    }
}

ext {
    crashlyticsVersion = '2.9.5@aar'
    firebaseVersion = '17.3.4'
    googleAuthVersion = '15.0.0'
    googleSafetynetVersion = '16.0.0'
    googleApiClientVersion = '1.22.0'
}

dependencies {
    implementation project(path: 'MemorizingTrustManager', configuration: 'default')
    implementation project(path: 'emojicon', configuration: 'default')

    // smack
    implementation 'com.github.str4tocaster.smack:smack-android-extensions:ffb99cc'
    implementation 'com.github.str4tocaster.smack:smack-experimental:ffb99cc'
    implementation 'com.github.str4tocaster.smack:smack-tcp:ffb99cc'
    implementation 'com.github.str4tocaster.smack:smack-resolver-dnsjava:ffb99cc'

    // android support
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.0'

    // firebase
    prodImplementation "com.google.firebase:firebase-messaging:$firebaseVersion"
    betaImplementation "com.google.firebase:firebase-messaging:$firebaseVersion"
    vipImplementation "com.google.firebase:firebase-messaging:$firebaseVersion"
    devImplementation "com.google.firebase:firebase-messaging:$firebaseVersion"

    // otr
    implementation 'com.github.redsolution:otr4j:custom-SNAPSHOT'

    // barcode scanner
    implementation 'com.google.zxing:android-integration:3.3.0'

    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.5.0'

    // rx java
    implementation 'io.reactivex:rxandroid:1.1.0'
    implementation 'io.reactivex:rxjava:1.1.0'

    // social
    implementation 'com.facebook.android:facebook-login:4.36.1'
    implementation 'com.twitter.sdk.android:twitter:3.0.0'

    // google safetynet (recaptcha)
    prodImplementation "com.google.android.gms:play-services-safetynet:$googleSafetynetVersion"
    betaImplementation "com.google.android.gms:play-services-safetynet:$googleSafetynetVersion"
    devImplementation "com.google.android.gms:play-services-safetynet:$googleSafetynetVersion"
    vipImplementation "com.google.android.gms:play-services-safetynet:$googleSafetynetVersion"

    // google auth
    prodImplementation "com.google.android.gms:play-services-auth:$googleAuthVersion"
    betaImplementation "com.google.android.gms:play-services-auth:$googleAuthVersion"
    devImplementation "com.google.android.gms:play-services-auth:$googleAuthVersion"
    vipImplementation "com.google.android.gms:play-services-auth:$googleAuthVersion"

    // google-api-client
    prodImplementation ("com.google.api-client:google-api-client-android:$googleApiClientVersion") { exclude module: 'httpclient' }
    betaImplementation ("com.google.api-client:google-api-client-android:$googleApiClientVersion") { exclude module: 'httpclient' }
    devImplementation ("com.google.api-client:google-api-client-android:$googleApiClientVersion") { exclude module: 'httpclient' }
    vipImplementation ("com.google.api-client:google-api-client-android:$googleApiClientVersion") { exclude module: 'httpclient' }

    // okhttp
    implementation 'com.squareup.okhttp3:okhttp:3.5.0'

    // event bus
    implementation 'org.greenrobot:eventbus:3.0.0'

    // commons-io
    implementation 'commons-io:commons-io:2.6'

    // bouncycastle
    implementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.58'

    // ui
    implementation 'me.relex:circleindicator:1.2.2@aar'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    implementation 'ru.egslava:MaskedEditText:1.0.5'
    implementation 'eu.davidea:flexible-adapter:5.0.0-rc4'
    implementation 'com.soundcloud.android:android-crop:1.0.1@aar'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.melnykov:floatingactionbutton:1.3.0'

    // crashlytics
    prodImplementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion") { transitive = true }
    betaImplementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion") { transitive = true }
    vipImplementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion") { transitive = true }
    devImplementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion") { transitive = true }

    // leak canary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    // block canary
    debugImplementation 'com.github.markzhai:blockcanary-android:1.5.0'
    releaseImplementation 'com.github.markzhai:blockcanary-no-op:1.5.0'

    // test
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.0"
    testImplementation "org.robolectric:shadows-multidex:4.0.1"
}
apply plugin: 'com.google.gms.google-services'

configurations {
    all*.exclude group: 'xpp3', module: 'xpp3'
    all*.exclude group: 'com.google.guava', module:'guava-jdk5'
}
